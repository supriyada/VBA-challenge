Option Explicit
Sub multiple_year_stock_data()

'Declaring variables
Dim ticker As String
Dim ticker_count As Integer
Dim i As Double
Dim close_price As Double
Dim open_price As Double
Dim yearly_percent_change As Double
Dim yearly_change As Double
Dim greatest_Increase As Double
Dim greatest_decrease As Double
Dim greatest_volume As Double
Dim stat_inc_ticker As String
Dim stat_dec_ticker As String
Dim stat_vol_ticker As String
Dim total_stock_volume As Double

Dim last_row As Double

Dim work_sheet As Worksheet

For Each work_sheet In Worksheets

    'Initialise values
    total_stock_volume = 0
    ticker_count = 2
    greatest_Increase = 0
    greatest_decrease = 0
    greatest_volume = 0

    'Set the column headers
    work_sheet.Range("J1:M1").Value = Array("Ticker", "Yearly Change ($)", "Percent Change", "Total Stock Volume")


    'Get the row count of the work_sheet
    last_row = work_sheet.Cells(Rows.Count, 1).End(xlUp).Row

    open_price = work_sheet.Range("C2").Value

    For i = 2 To last_row
    'Compare the immediate next row for new ticker symbol
    
    If work_sheet.Cells(i, 1).Value = work_sheet.Cells(i + 1, 1).Value Then
        
        'Calculate the total sum of stock volume of the current ticker symbol picked
        total_stock_volume = total_stock_volume + work_sheet.Cells(i, 7).Value
        close_price = work_sheet.Cells(i + 1, 6).Value
    Else
    
        'Get the ticker symbol
        ticker = work_sheet.Cells(i, 1).Value
        
        'Find the total stock volume
        total_stock_volume = total_stock_volume + work_sheet.Cells(i, 7).Value
        
        'Populate the calculated values in the new cells
        work_sheet.Range("J" & ticker_count).Value = ticker
        work_sheet.Range("M" & ticker_count).Value = total_stock_volume
        
        'Checking for the greatest_total_stock_volume
        If (work_sheet.Range("M" & ticker_count).Value > greatest_volume) Then
                greatest_volume = work_sheet.Range("M" & (ticker_count)).Value
                stat_vol_ticker = work_sheet.Range("J" & (ticker_count)).Value
        End If
        
        'Calculate yearly difference
        yearly_change = close_price - open_price
        work_sheet.Range("K" & ticker_count).Value = yearly_change
        
        'Set color code for yearly change in price
        If yearly_change <= 0 Then
            work_sheet.Range("K" & ticker_count).Interior.Color = vbRed
        Else
            work_sheet.Range("K" & ticker_count).Interior.Color = vbGreen
        End If
        
        'Calculate yearly percent change
        If close_price = 0 And open_price = 0 Then
            work_sheet.Range("L" & ticker_count).Value = 0
        Else
            If open_price = 0 Then
                yearly_percent_change = yearly_change
            Else
                yearly_percent_change = yearly_change / open_price
            End If
            
            'Set the yearly_percent_change value
            work_sheet.Range("L" & ticker_count).Value = yearly_percent_change
                        
            'Checking for the greatest_percent_increase
            If (work_sheet.Range("L" & ticker_count).Value > greatest_Increase) Then
                greatest_Increase = work_sheet.Range("L" & (ticker_count)).Value
                stat_inc_ticker = work_sheet.Range("J" & (ticker_count)).Value
            End If
            
            'Checking for the greatest_percent_decrease
            If (work_sheet.Range("L" & ticker_count).Value < greatest_decrease) Then
                greatest_decrease = work_sheet.Range("L" & (ticker_count)).Value
                stat_dec_ticker = work_sheet.Range("J" & (ticker_count)).Value
            End If
        End If
        
        'Reset Open_price to next ticker
        open_price = work_sheet.Cells(i + 1, 3).Value
        
        'Reset the stock volume to 0 for the next ticker symbol
        total_stock_volume = 0

        'Increment the row value to populate the next ticker symbol
        ticker_count = ticker_count + 1
        
    End If
    
Next i

work_sheet.Range("Q2").Value = "Greatest % Increase"
work_sheet.Range("Q3").Value = "Greatest % Decrease"
work_sheet.Range("Q4").Value = "Greatest Total Volume"
work_sheet.Range("R1:S1").Value = Array("Ticker", "Value")
work_sheet.Range("J1:S1").Font.Bold = True
work_sheet.Range("Q2:Q4").Font.Bold = True
work_sheet.Range("L:L").NumberFormat = "0.00%"
work_sheet.Range("S2").NumberFormat = "0.00%"
work_sheet.Range("S3").NumberFormat = "0.00%"

'Set the greatest percentage increase and its ticker symbol
work_sheet.Range("S2").Value = greatest_Increase
work_sheet.Range("R2").Value = stat_inc_ticker

'Set the greatest percentage decrease and its ticker symbol
work_sheet.Range("S3").Value = greatest_decrease
work_sheet.Range("R3").Value = stat_dec_ticker

'Set the greatest total volume and its ticker symbol
work_sheet.Range("S4").Value = greatest_volume
work_sheet.Range("R4").Value = stat_vol_ticker

work_sheet.Columns.AutoFit

Next work_sheet
End Sub
